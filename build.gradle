plugins {
    id 'java'
    id("com.github.johnrengelman.shadow") version "8.1.1"
}

group = 'com.ordwen'
version = '0.0.1-BETA'

repositories {
    mavenCentral()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = 'jitpack-repo'
        url = 'https://jitpack.io'
    }
    maven {
        name = 'enginehub-repo'
        url = "https://maven.enginehub.org/repo/"
    }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:1.20-R0.1-SNAPSHOT"

    /* database management */
    compileOnly 'com.zaxxer:HikariCP:5.0.1'
    compileOnly 'com.h2database:h2:2.1.214'

    /* custom items */
    compileOnly 'com.github.oraxen:oraxen:1.157.0'
    compileOnly 'com.github.LoneDev6:API-ItemsAdder:3.5.0b'

    compileOnly 'com.sk89q.worldguard:worldguard-bukkit:7.0.9'

    implementation 'org.jetbrains:annotations:24.1.0'
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
